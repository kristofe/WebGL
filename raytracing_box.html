<html>

<head>
<title>WebGL</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<script type="text/javascript" src="matrix.js"></script>


<script id="shader-fs-raytracer" type="x-shader/x-fragment">
    precision highp float;
    varying vec2 vUV;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform float uTime;

    struct Ray {
      vec3 origin;
      vec3 dir;
    };

    struct Plane {
      vec4 p;
      vec4 color;
    };


    struct AABB {
      vec3 min;
      vec3 max;
      vec4 color;
    };

    struct HitInfo {
      bool hit;
      vec3 t;
      vec3 position;
      vec3 normal;
      vec3 reflection;
      vec4 color;
    };

    struct Sphere {
      vec4 posRadius;
      vec4 color;
    };

    struct Cube {
      vec4 unitPlanes[6];
      vec4 planes[6];
      vec4 color;
      mat4 xForm;
    };

    struct Light {
      vec3 direction;
      vec4 ambient;
      vec4 c0;
      vec4 c1;
    };

    mat4 tsMatrix(vec3 t, vec3 s) {
        return mat4(s.x,0.,0.,0., 0.,s.y,0.,0., 0.,0.,s.z,0., t.x,t.y,t.z,1.);
    } 

    mat4 inverseTSMatrix(vec3 t, vec3 s) {
        vec3 T = -t/s;
        vec3 S = 1.0/s;
        return mat4(S.x,0.,0.,0., 0.,S.y,0.,0., 0.,0.,S.z,0., T.x,T.y,T.z,1.);
    } 

    Cube createUnitCube(vec4 color, mat4 m) {
      Cube c;
      c.unitPlanes[0] = vec4( 1.0, 0.0, 0.0, -1.0);
      c.unitPlanes[1] = vec4(-1.0, 0.0, 0.0, -1.0);
      c.unitPlanes[2] = vec4( 0.0, 1.0, 0.0, -1.0);
      c.unitPlanes[3] = vec4( 0.0,-1.0, 0.0, -1.0);
      c.unitPlanes[4] = vec4( 0.0, 0.0, 1.0, -1.0);
      c.unitPlanes[5] = vec4( 0.0, 0.0,-1.0, -1.0);
      for(int i = 0; i < 6; i++){
        c.planes[i] = c.unitPlanes[i] * m;
      }
      c.color = color;
      //c.xForm = m;
      return c;
    }

    AABB createAABB(vec3 min, vec3 max, vec4 color){
      AABB b;
      b.min = min;
      b.max = max;
      b.color = color;
      return b;
    }


    //This was taken from http://prideout.net/blog/?p=64
    bool intersectBox(Ray r, AABB aabb, out float t_min, out float t_max)
    {
        vec3 invR = 1.0 / r.dir;
        vec3 tbot = invR * (aabb.min-r.origin);
        vec3 ttop = invR * (aabb.max-r.origin);
        vec3 tmin = min(ttop, tbot);
        vec3 tmax = max(ttop, tbot);
        vec2 t = max(tmin.xx, tmin.yz);
        t_min = max(t.x, t.y);
        t = min(tmax.xx, tmax.yz);
        t_max = min(t.x, t.y);
        return t_min <= t_max;
    }

    float intersectRayPlane(Ray ray, vec4 L){
      vec4 W = vec4(ray.dir,0.0);
      vec4 V = vec4(ray.origin,1.0);
      float LDOTV, LDOTW;
      float t = -1e20;
      LDOTV = dot(L,V);
      LDOTW = dot(L,W);
      if(LDOTW < 0.0)
      {
        t = -LDOTV/LDOTW;
      }
      return t;
    }
    HitInfo intersectPlane(Ray ray, Plane plane, Light light){
      HitInfo hitInfo;
      hitInfo.hit = false;
      hitInfo.t = vec3(1e20,0.0, 0.0);
      vec4 W = vec4(ray.dir,0.0);
      vec4 V = vec4(ray.origin,1.0);
      float LDOTV, LDOTW;
      float t;
      float eps = 0.0000;

      vec4 L = plane.p;
      LDOTV = dot(L,V);
      LDOTW = dot(L,W);
      if(LDOTW < 0.0 - eps) {
        t = -LDOTV/LDOTW;
        hitInfo.t = vec3(t);
        hitInfo.hit = true;
        hitInfo.position = ray.origin + t*ray.dir;
        hitInfo.normal = normalize(L.xyz);
        hitInfo.reflection = reflect(hitInfo.normal, ray.dir);

        //hitInfo.color = vec4(hitInfo.normal.xyz, 1.0); 
           
        hitInfo.color = plane.color *(light.ambient + (1.0 - light.ambient) *
                                max(0.0,dot(hitInfo.normal, light.direction)));

      }
      return hitInfo;
    }

    HitInfo intersectCube(Ray ray, Cube box, Light light) {
      HitInfo hitInfo;
      hitInfo.hit = false;
      bool knownMiss = false;
      vec4 p0, p1, L;
      float LDOTV, LDOTW, t;
      float eps = 0.0001;

      vec4 W = vec4(ray.dir,0.0);
      vec4 V = vec4(ray.origin,1.0);

      //Initialize the intersections to values that should be out of range
      float tenter[6];
      float texit[6];
      tenter[0]= tenter[1]= tenter[2]= tenter[3]= tenter[4]= tenter[5] = -1e20;
      texit[0]= texit[1]= texit[2]= texit[3]= texit[4]= texit[5] =  1e20;

      //Loop through each plane. testing whether entering or exiting
      for(int i = 0; i < 6; i++){
        L = box.planes[i];
        LDOTV = dot(L,V);
        LDOTW = dot(L,W);
        if(LDOTW < 0.0 - eps) {
          t = -LDOTV/LDOTW;
          tenter[i] = t;
        }else if(LDOTW > 0.0 + eps) {
          t = -LDOTV/LDOTW;
          texit[i] = t;
        }
        else if(abs(LDOTW) <= eps && LDOTV > 0.0){
          knownMiss = true;
        }
      }

      float t_near = tenter[0];
      float t_far = texit[0];
      vec4 hitPlane = box.planes[0];

      //Calculate max t_enter and min t_far
      for(int i = 1; i < 6; i++){
        if(tenter[i] > t_near){
            t_near = tenter[i];
            hitPlane = box.planes[i];
        } 
        if(texit[i] < t_far){
            t_far = texit[i];
        } 
      }

      hitInfo.t = vec3(1e20, -1e20, 0.0);
      if(knownMiss == false && t_near > 0.0 && t_near < t_far){
        //We hit the cube
        hitInfo.hit = true;
        hitInfo.t = vec3(t_near, t_far,0.0);
        hitInfo.position = ray.origin + t_near*ray.dir;
        hitInfo.normal = normalize(hitPlane.xyz);
        hitInfo.reflection = reflect(hitInfo.normal, ray.dir);
        hitInfo.color = box.color *(light.ambient + (1.0 - light.ambient) *
                                max(0.0,dot(hitInfo.normal, light.direction)));
      }

      return hitInfo;

    }

    HitInfo intersectSphere(Ray ray, Sphere sphere, Light light) {
      //Sphere is pos(x,y,z) and radius(w)
      HitInfo hitInfo;
      hitInfo.hit = false;
      
      //Color if nothing is hit.  Use dir.y
      float angle = ray.dir.y*0.5 + 0.5;
      hitInfo.color = mix(light.c0, light.c1, angle);

      float radius = sphere.posRadius.w;
      vec3 center = sphere.posRadius.xyz;

	    vec3 diff = ray.origin - center;
      //float a = dot(dir,dir);  //should be one because dir is normalized
      float b = dot(2.0*ray.dir,diff);
      float c = dot(diff,diff) - radius * radius;
      float disc = b * b - 4.0 * c;
      if (disc < 0.0)
      {
        hitInfo.t = vec3(0.0);
        return hitInfo;
      }
      disc = sqrt(disc);
      hitInfo.t.x = (-b - disc)/(2.);
      hitInfo.t.y = (-b + disc)/(2.);
      float test = 0.0;
      if(hitInfo.t.x < 0.0){
        hitInfo.t.x = 0.0;
        test += 1.0;
      }
      if(hitInfo.t.y < 0.0) {
        hitInfo.t.y = 0.0;
        test += 1.0;
      }
      hitInfo.t.x = min(hitInfo.t.x,hitInfo.t.y); 
      hitInfo.t.y = max(hitInfo.t.x,hitInfo.t.y); 
      hitInfo.t.z = 2.0 - test;

      if(hitInfo.t.z > 0.0){
        hitInfo.hit = true;
        hitInfo.position = ray.origin + (hitInfo.t.x * ray.dir);
        hitInfo.normal = normalize(hitInfo.position - sphere.posRadius.xyz);
        hitInfo.reflection = reflect(ray.dir, hitInfo.normal);
        hitInfo.color = sphere.color *(light.ambient + (1.0 - light.ambient) *
                                max(0.0,dot(hitInfo.normal, light.direction)));
      }
      

      return hitInfo;

    }

    vec3 calcReflection(vec3 dir, vec3 norm) {
      return (2.0 * dot(-dir , norm) * norm) + dir;
    }

    mat4 makeTranslationMatrix(vec3 s) {

      return mat4(
        1.0, 0.0, 0.0, 0.0,
        0.0, 1.0, 0.0, 0.0,
        0.0, 0.0, 1.0, 0.0,
        s.x, s.y, s.z, 1.0
      );
    }
    mat4 makeScaleMatrix(vec3 s) {

      return mat4(
        s.x, 0.0, 0.0, 0.0,
        0.0, s.y, 0.0, 0.0,
        0.0, 0.0, s.z, 0.0,
        0.0, 0.0, 0.0, 1.0
      );
    }

    mat4 makeXRotationMatrix(float angleInRadians) {
      float c = cos(angleInRadians);
      float s = sin(angleInRadians);

      return mat4(
        1.0, 0.0, 0.0, 0.0,
        0.0,  c ,  s , 0.0,
        0.0, -s ,  c , 0.0,
        0.0, 0.0, 0.0, 1.0
      );
    }

    mat4 makeYRotationMatrix(float angleInRadians) {
      float c = cos(angleInRadians);
      float s = sin(angleInRadians);

      return mat4(
         c , 0.0,  -s, 0.0,
        0.0, 1.0, 0.0, 0.0,
         s , 0.0,  c , 0.0,
        0.0, 0.0, 0.0, 1.0
      );
    }

    mat4 makeZRotationMatrix(float angleInRadians) {
      float c = cos(angleInRadians);
      float s = sin(angleInRadians);

      return mat4(
         c ,   s,  0.0, 0.0,
         -s,   c,  0.0, 0.0,
        0.0, 0.0, 1.0, 0.0,
        0.0, 0.0, 0.0, 1.0
      );
    }

   mat4 transpose(mat4 m){
     mat4 o;
     for(int c = 0; c < 4; c++)
     for(int r = 0; r < 4; r++)
       o[c][r] = m[c][r]; 
     return o;
   }


//-----------------------------------------------------------------------------
    struct Quadric{
      vec4 cap1;
      vec4 cap2;
      vec4 xformCap1;
      vec4 xformCap2;
      vec4 color;
      float cA,cB,cC,cD,cE,cF,cG,cH,cI,cJ;
      mat4 m;
    };

   void initCylinderCoeffs(inout Quadric q){
     q.cA = 1.0;
     q.cB = 0.0;
     q.cC = 1.0;
     q.cD = 0.0;
     q.cE = 0.0;
     q.cF = 0.0;
     q.cG = 0.0;
     q.cH = 0.0;
     q.cI = 0.0;
     q.cJ = -1.0;
   }

   void initConeCoeffs(inout Quadric q) {
     q.cA = 1.0;
     q.cB = 1.0;
     q.cC = -1.0;
     q.cD = 0.0;
     q.cE = 0.0;
     q.cF = 0.0;
     q.cG = 0.0;
     q.cH = 0.0;
     q.cI = 0.0;
     q.cJ = -1.0;
   }

   Quadric createQuadric(bool createCylinder, vec4 cap1, vec4 cap2, vec4 color){
      Quadric q;
      q.cap1 = cap1;
      q.cap2 = cap2;
      q.color = color;
      if(createCylinder){
        initCylinderCoeffs(q);
      }else{
        initConeCoeffs(q);
      }
     
      q.m = mat4( 
          q.cA, q.cF, q.cE, q.cG,
           0.0, q.cB, q.cD, q.cH,
           0.0,  0.0, q.cC, q.cI,
           0.0,  0.0,  0.0, q.cJ
          );
      return q;
   }

   void transformQuadric(inout Quadric q, mat4 mat, mat4 mCap){
     q.xformCap1 = q.cap1 * mCap;
     q.xformCap2 = q.cap2 * mCap; 
     mat4 m = transpose(mat) * q.m * mat;

     q.cA = m[0][0];
     q.cB = m[1][1];
     q.cC = m[2][2];
     q.cD = m[2][1]+m[1][2];
     q.cE = m[2][0]+m[0][2];
     q.cF = m[1][0]+m[0][1];
     q.cG = m[3][0]+m[0][3];
     q.cH = m[3][1]+m[1][3];
     q.cI = m[3][2]+m[2][3];
     q.cJ = m[3][3];
   }


   HitInfo intersectQuadric(Ray ray, Quadric q, Light light){
     HitInfo hitInfo;
     vec4 W = vec4(ray.dir,0.0);
     vec4 V = vec4(ray.origin,1.0);
     vec4 WSQR = W*W;
     vec4 VSQR = V*V;

     float A = q.cA*WSQR.x 
              + q.cB*WSQR.y 
              + q.cC*WSQR.z 
              + q.cD*W.y*W.z
              + q.cE*W.z*W.x 
              + q.cF*W.x*W.y;

     float B = 2.0*(q.cA*V.x*W.x + q.cB*V.y*W.y + q.cC*V.z*W.z)
                + q.cD*(V.y*W.z+V.z*W.y) 
                + q.cE*(V.z*W.x+V.x*W.z) 
                + q.cF*(V.x*W.y+V.y*W.x) 
                + q.cG*W.x 
                + q.cH*W.y 
                + q.cI*W.z; 

     float C =   q.cA*VSQR.x
               + q.cB*VSQR.y
               + q.cC*VSQR.z
               + q.cD*V.y*V.z
               + q.cE*V.z*V.x
               + q.cF*V.x*V.y
               + q.cG*V.x
               + q.cH*V.y
               + q.cI*V.z
               + q.cJ;

      bool missed = false;

      if(A != 0.0){
         float disc = B * B - 4.0 * A * C;
         if (disc < 0.0)
         {
           hitInfo.t = vec3(1e20, 1e20,0.0);
           return hitInfo;
         }
         disc = sqrt(disc);
         float t1 = (-B - disc)/(2.0*A);
         float t2 = (-B + disc)/(2.0*A);

         if(t1 < 0.0){
            if(t2 < 0.0) {
               hitInfo.t = vec3(1e20, 1e20,0.0);
               return hitInfo;
            }else{
               hitInfo.t.x = t2;
               hitInfo.t.z = 1.0;
            }
         }else{
            if(t2 < 0.0) {
               hitInfo.t.x = t1;
               hitInfo.t.z = 1.0;
            }else{
               if(t2 < t1){
                 //swap
                 float tmp = t1;
                 t1 = t2;
                 t2 = tmp;
               }
               hitInfo.t.x = t1;
               hitInfo.t.y = t2;
               hitInfo.t.z = 2.0;

            }
         }
      }else{
         hitInfo.t.x = -C/B;
         hitInfo.t.z = 1.0;
      }
      
      //Now check planes
      float tc1 = intersectRayPlane(ray, q.xformCap1);

      float tc2 = intersectRayPlane(ray, q.xformCap2);
      float nearestCapT = min(tc1, tc2);
      float farthestCapT = max(tc1, tc2);

      float nearestT = max(nearestCapT, hitInfo.t.x);
      float farthesT = min(farthestCapT, hitInfo.t.x);

      bool useOtherNormal = false;
      vec3 otherNormal;
      
      hitInfo.t.y = nearestT;
      if(tc1 == nearestT){
         useOtherNormal = true;
         otherNormal = normalize(q.xformCap1.xyz);
      }
      else if(tc2 == nearestT){
         useOtherNormal = true;
         otherNormal = normalize(q.xformCap2.xyz);
      }
      //Plane p;
      //p.p = q.xformCap1;
      //p.color = q.color;
      //hitInfo = intersectPlane(ray,p, light); 
      missed = true;
      vec3 pos = ray.origin + (hitInfo.t.x * ray.dir);
      if(
         dot(pos, normalize(q.xformCap1.xyz)) > q.xformCap1.w
         && 
         dot(pos, normalize(q.xformCap2.xyz)) > q.xformCap2.w
         )
      {
         missed = false;
         //hitInfo.t.x = 0.0;
      }
      hitInfo.hit = !missed;
     

      if(hitInfo.t.x > 0.0 && missed == false){
        hitInfo.hit = true;
        hitInfo.position = ray.origin + (hitInfo.t.x * ray.dir);
        vec3 p = hitInfo.position;
        if(useOtherNormal == false){
           hitInfo.normal = normalize(
                             vec3(
                               2.0*q.cA*p.x + q.cE*p.z + q.cF*p.y + q.cG,
                               2.0*q.cB*p.y + q.cD*p.z + q.cF*p.x + q.cH,
                               2.0*q.cC*p.z + q.cD*p.y + q.cE*p.x + q.cI 
                             )
                            );
        } else {
           hitInfo.normal = otherNormal;
        }
        hitInfo.reflection = reflect(ray.dir, hitInfo.normal);
        //hitInfo.color =  vec4(hitInfo.normal,1.0);
        hitInfo.color = q.color * (light.ambient + (1.0 - light.ambient) * max(0.0,dot(hitInfo.normal, light.direction)));
      }

      return hitInfo;
   }
//-----------------------------------------------------------------------------
    void main(void) {
      float trans = sin(uTime);
      //globals
      Light light;
      light.direction = normalize(vec3(0.0,-1.0, 1.0));
      light.ambient = vec4(0.1, 0.1, 0.1, 1.0);
      light.c1 = vec4(0.0, 0.0, 0.0, 1.0);
      light.c0 = vec4(0.6, 0.6, 0.6, 1.0);

      float focalLength = 5.0;
      vec3 viewPoint = vec3(0.0,0.0, focalLength);
      vec3 rayDirection = normalize(vec3(vUV.x-0.5, 0.5-vUV.y, -focalLength));
      Ray ray = Ray(viewPoint, rayDirection);

      //if we hit nothing use the gradient
      float angle = 1.0 - vUV.y;
      vec4 color = mix(light.c0, light.c1, angle);

      HitInfo hit;
      hit.t = vec3(1e20);

      mat4 ident = mat4(
          1., 0., 0., 0.,
          0., 1., 0., 0.,
          0., 0., 1., 0.,
          0., 0., 0., 1.
          );

      Quadric cyl = createQuadric(
                                true, 
                                vec4( 0.,  1., 0., -0.2), 
                                vec4( 0., -1., 0., -0.2), 
                                vec4( .6, .6, 1.,  1.)
                                );
      mat4 mR1 = makeZRotationMatrix(0.0);
      mat4 mR1Inv = makeZRotationMatrix(-0.0);
      mat4 mR2 = makeYRotationMatrix(uTime);
      mat4 mR = mR1;
      mat4 mRInv = mR1Inv;
      mat4 mS = makeScaleMatrix(vec3(10.0, 10.0, 10.0)); //Inverse of 0.1
      mat4 mSInv = makeScaleMatrix(vec3(0.1, 0.1, 0.1)); //Inverse of 0.1
      mat4 mX = makeTranslationMatrix(vec3(0.0,0.0, 0.0));
      mat4 mXInv = makeTranslationMatrix(vec3(0.0,0.0, 0.0));
      //mat4 m = mR * mS;
      mat4 m = mS * mX * mR;
      mat4 mInv = mSInv * mXInv * mRInv;
      transformQuadric(cyl, m, mInv);

      hit = intersectQuadric(ray, cyl, light);
      if(hit.hit) {
        color = hit.color;
      }

      mat4 R1 = makeXRotationMatrix(uTime);
      mat4 R2 = makeYRotationMatrix(uTime);
      mat4 S = makeScaleMatrix(vec3(10.0)); //Inverse of 0.1
      mat4 mm = S * R1 * R2;
      Cube cube = createUnitCube(vec4(0.0, 0.6, 1.0, 1.0), mm);

      float minT = hit.t.x;
      HitInfo hitCube = intersectCube(ray, cube, light);
      if(hitCube.hit && hitCube.t.x < hit.t.x) {
        color = hitCube.color;
      }



      gl_FragColor = color;
    }


</script>

<script id="shader-vs" type="x-shader/x-vertex">
    //Data from bufferData
    attribute vec3 aVertexPosition;
    attribute vec2 aVertexUV;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform float uTime;

    //These will be passed to the fragment shader and will be interpreted per
    //pixel
    varying vec2 vUV;

    void main(void) {
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        //The line below will ignore any translation of perspective matrix
        //gl_Position =  vec4(aVertexPosition, 1.0);
        vUV = aVertexUV;
    }
</script>



<script type="text/javascript">

    var gl;
    function initGL(canvas) {
        try {
            gl = canvas.getContext("webgl") ||
                 canvas.getContext("experimental-webgl");
            gl.viewportWidth = canvas.width;
            gl.viewportHeight = canvas.height;
        } catch (e) {
        }
        if (!gl) {
            alert("Could not initialise WebGL, sorry :-(");
        }
    }


    function getShader(gl, id) {
        var shaderScript = document.getElementById(id);
        if (!shaderScript) {
            return null;
        }

        var str = "";
        var k = shaderScript.firstChild;
        while (k) {
            if (k.nodeType == 3) {
                str += k.textContent;
            }
            k = k.nextSibling;
        }

        var shader;
        if (shaderScript.type == "x-shader/x-fragment") {
            shader = gl.createShader(gl.FRAGMENT_SHADER);
        } else if (shaderScript.type == "x-shader/x-vertex") {
            shader = gl.createShader(gl.VERTEX_SHADER);
        } else {
            return null;
        }

        gl.shaderSource(shader, str);
        gl.compileShader(shader);

        if (!gl.getShaderParameter(shader, gl.COMPILE_STATUS)) {
          var errStr = gl.getShaderInfoLog(shader);
            console.error(errStr);
            alert(errStr);
            return null;
        }

        return shader;
    }


    var raytracerShaderProgram;
    var triangleShaderProgram;

    function initShader(fragment_shadername, vertex_shadername) {
        var fragmentShader = getShader(gl, fragment_shadername);
        var vertexShader = getShader(gl, vertex_shadername);

        var shaderProgram = gl.createProgram();
        gl.attachShader(shaderProgram, vertexShader);
        gl.attachShader(shaderProgram, fragmentShader);
        gl.linkProgram(shaderProgram);

        if (!gl.getProgramParameter(shaderProgram, gl.LINK_STATUS)) {
            alert("Could not initialise shaders");
        }

        gl.useProgram(shaderProgram);

        shaderProgram.vertexPositionAttribute = 
                        gl.getAttribLocation(shaderProgram, "aVertexPosition");
        gl.enableVertexAttribArray(shaderProgram.vertexPositionAttribute);

        shaderProgram.vertexUVAttribute = 
                              gl.getAttribLocation(shaderProgram, "aVertexUV");
        gl.enableVertexAttribArray(shaderProgram.vertexUVAttribute);

        shaderProgram.pMatrixUniform =gl.getUniformLocation(shaderProgram, 
                                                            "uPMatrix");
        shaderProgram.mvMatrixUniform =gl.getUniformLocation(shaderProgram, 
                                                             "uMVMatrix");

        shaderProgram.currTime =gl.getUniformLocation(shaderProgram, "uTime");

        return shaderProgram;
    }


    var mvMatrix;
    var mvMatrixTriangle;
    var pMatrix;

    function setUniforms(shader, mv, p) {
        gl.uniformMatrix4fv(shader.pMatrixUniform, false, p);
        gl.uniformMatrix4fv(shader.mvMatrixUniform, false, mv);
        gl.uniform1f(shader.currTime, currTime);

    }



    var raytracerVertexBuffer;

    function initBuffers() {
      //Create Square Position Buffer
      var vertices = [
           1.0,  1.0,  0.0, 1.0, 1.0, //x, y, z, u, v
          -1.0,  1.0,  0.0, 0.0, 1.0,
           1.0, -1.0,  0.0, 1.0, 0.0,
          -1.0, -1.0,  0.0, 0.0, 0.0
      ];

      raytracerVertexBuffer = createVertexBuffer(vertices,5);
    }


    function drawScene() {
        gl.viewport(0, 0, gl.viewportWidth, gl.viewportHeight);
        gl.clear(gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT);


        pMatrix = identity();
        mvMatrix = identity();

        pMatrix = ortho(-1, 1, -1, 1, 0.01, 10);
        /*
        pMatrix = makePerspective(
                                 45 * Math.PI/180.0, 
                                 gl.viewportWidth / gl.viewportHeight, 
                                 0.1, 
                                 100.0
                                 );
        */
        mvMatrix = makeTranslation(0.0, 0.0, -0.01);

        gl.useProgram(raytracerShaderProgram);
        gl.bindBuffer(gl.ARRAY_BUFFER, raytracerVertexBuffer);
        gl.vertexAttribPointer(
                              raytracerShaderProgram.vertexPositionAttribute,
                              raytracerVertexBuffer.positionElementCount, 
                              gl.FLOAT, 
                              false, 
                              raytracerVertexBuffer.stride, 
                              raytracerVertexBuffer.positionOffset
                              );
        gl.vertexAttribPointer(
                              raytracerShaderProgram.vertexUVAttribute, 
                              raytracerVertexBuffer.uvElementCount, 
                              gl.FLOAT, 
                              false, 
                              raytracerVertexBuffer.stride, 
                              raytracerVertexBuffer.uvOffset 
                              );
        setUniforms(raytracerShaderProgram, mvMatrix, pMatrix);
        gl.drawArrays(
                      gl.TRIANGLE_STRIP, 
                      0, 
                      raytracerVertexBuffer.numItems
                      );
    }


    var mouse = [250,250];
    var mouseRadius = 10;
    var currentAngle = [0,0]; // [x-axis, y-axis] degrees
    var startTime = 0.0;
    var currTime = 0.0;

    function webGLStart() {
        var canvas = document.getElementById("webgl-canvas");
        startTime = (new Date).getTime();

        initGL(canvas);
        raytracerShaderProgram = initShader("shader-fs-raytracer","shader-vs");
        initBuffers();
        
        initEventHandlers(canvas, mouse, currentAngle);

        gl.clearColor(0.0, 0.0, 0.0, 1.0);
        gl.disable(gl.DEPTH_TEST);
        //gl.enable(gl.DEPTH_TEST);


        var tick = function() {
          currTime = ((new Date).getTime() - startTime)*0.001;
          drawScene();
          requestAnimationFrame(tick,canvas);
        };
        tick();
    }

    function initEventHandlers(canvas, mousePosition, currentAngle)
    {
      var lastX = -1;
      var lastY = -1;
      var dragging = false;

      canvas.onmousedown = function(ev) {  //Mouse is pressed
        var x = ev.clientX;
        var y = ev.clientY;

        var rect = ev.target.getBoundingClientRect();
        if(rect.left <= x 
            && x <= rect.right 
            && rect.top <= y 
            && y <= rect.bottom
          ) {
          lastX = x; 
          lastY = y;
          mousePosition[0] = x;
          mousePosition[1] = canvas.height - y;
          dragging = true;

        }
      };

      canvas.onmouseup = function(ev){ //Mouse is released
        dragging = false;
      }

      canvas.onmousemove = function(ev) { //Mouse is moved
        var x = ev.clientX;
        var y = ev.clientY;
        if(dragging) {
          //put some kind of dragging logiv in here
          //Here is a roation example
          var factor = 100/canvas.height;
          var dx = factor * (x - lastX);
          var dy = factor * (y - lastY);
          //Limit x-axis roation angle to -90 to 90 degrees
          currentAngle[0] = Math.max(Math.min(currentAngle[0] + dy, 90), -90);
          currentAngle[1] = currentAngle[1] + dx;

          mousePosition[0] = x;
          mousePosition[1] = canvas.height - y;

        }
        lastX = x;
        lastY = y;

      }
    }


    function createGridMesh(n, m){
      var numVerts = n * m;
      var vertices = [];

      var xpos = 0.0;
      var ypos = 0.0;
      var zpos = 0.0;
      var xinc = 1/n;
      var yinc = 1/m;
      for(var j = 0; j < m; ++j){
        for(var i = 0; i < n; ++i){
          var lx = xpos;
          var rx = xpos + xinc;
          var dy = ypos; 
          var uy = ypos + yinc;

          var uvl = lx;
          var uvr = rx;
          var uvd = dy;
          var uvu = uy;

          /*
          var uvl = i;
          var uvr = i+1;
          var uvd = j+1;
          var uvu = j;
          */

          vertices.push(lx, dy, zpos, uvl, uvd);
          vertices.push(rx, dy, zpos, uvr, uvd);
          vertices.push(rx, uy, zpos, uvr, uvu);

          vertices.push(lx, dy, zpos, uvl, uvd);
          vertices.push(rx, uy, zpos, uvr, uvu);
          vertices.push(lx, uy, zpos, uvl, uvu);
          xpos += xinc;
        }
        ypos += yinc;
        xpos = 0.0;
      }
      console.log(vertices);
      console.log(n, m);

      return vertices;
    }

    function createVertexBuffer(vertArray, stride){

      
        var vertexBuffer = gl.createBuffer();
        gl.bindBuffer(gl.ARRAY_BUFFER, vertexBuffer);
        gl.bufferData(
                     gl.ARRAY_BUFFER, 
                     new Float32Array(vertArray), 
                     gl.STATIC_DRAW
                     );
        vertexBuffer.stride = stride*Float32Array.BYTES_PER_ELEMENT;
        vertexBuffer.positionElementCount = 3;
        vertexBuffer.positionOffset = 0;
        vertexBuffer.uvElementCount = 2;
        vertexBuffer.uvOffset = 3*Float32Array.BYTES_PER_ELEMENT;
        vertexBuffer.numItems = vertArray.length/stride;

        return vertexBuffer;
    }

</script>


</head>


<body onload="webGLStart();">
  <canvas id="webgl-canvas" style="border: none;" width="500" height="500">
  </canvas>

</body>

</html>


